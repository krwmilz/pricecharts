#!/usr/bin/env perl

use strict;
use warnings;

use Config::Grammar;
use Getopt::Std;
use File::Copy;
use PriceChart;
use Template;
use Time::Piece;
use URI::Escape;


my %args;
getopts("v", \%args);

$| = 1 if ($args{v});

my $cfg = get_config();
my $dbh = get_dbh($cfg->{http}, undef, $args{v});

my $work_dir =  $cfg->{http}{chroot} . $cfg->{http}{htdocs};
print "info: working dir: $work_dir\n" if ($args{v});

# rock a new template
my $config = {
	INTERPOLATE => 1, POST_CHOMP => 1, EVAL_PERL => 1,
	INCLUDE_PATH => "$work_dir/tt", OUTPUT_PATH => $work_dir
};
my $template = Template->new($config)
	|| die "template: . " . Template->error() . "\n";

#
# manufacturers/*
#
my $sql = "select distinct lower(manufacturer) from products";
my $manufacturers = $dbh->selectcol_arrayref($sql);

my ($num_manuf, $manufacturer_indexed) = (scalar @$manufacturers, 0);
print "info: gen manufacturers/ " if ($args{v});
for my $manufacturer (@$manufacturers) {
	# merge manufacturers that are spelled the same except for casing
	$sql = "select manufacturer from products where lower(manufacturer) = ?";
	my ($manufacturer_cased) = $dbh->selectrow_array($sql, undef, $manufacturer);

	# must have at least one price
	$sql = "select distinct manufacturer, part_num from prices where " .
		"lower(manufacturer) = ?";
	my $products = $dbh->selectall_arrayref($sql, undef, $manufacturer);
	$_->[2] = get_description($_->[0], $_->[1]) for (@$products);

	my $vars = {
		name => $manufacturer_cased, num => scalar @$products,
		products => $products,
	};
	$template->process("chart_list.tt", $vars,
		"manufacturers/$manufacturer.html")
		|| die "template: " . $template->error() . "\n";

	$manufacturer_indexed += @$products;
}
print "$num_manuf pages, $manufacturer_indexed indexed\n" if ($args{v});

#
# manufacturers.html
#
print "info: gen manufacturers.html\n" if ($args{v});

my $vars = { name => "Manufacturers", num => $num_manuf, links => $manufacturers };
$template->process("link_list.tt", $vars, "manufacturers.html")
	|| die "template: " . $template->error() . "\n";

#
# retailers/*
#
$sql = "select distinct retailer from prices";
my $retailers = $dbh->selectcol_arrayref($sql);

my ($num_retailers, $retailer_indexed) = (scalar @$retailers, 0);
print "info: gen retailers/ " if ($args{v});
for my $retailer (@$retailers) {
	my $retailer_lc = lc($retailer);

	# must have at least one price
	$sql = "select distinct manufacturer, part_num from prices where retailer = ?";
	my $products = $dbh->selectall_arrayref($sql, undef, $retailer);
	$_->[2] = get_description($_->[0], $_->[1]) for (@$products);

	my $vars = {
		name => $retailer, num => scalar @$products,
		products => $products,
	};
	$template->process("chart_list.tt", $vars, "retailers/$retailer_lc.html")
		|| die "template: " . $template->error() . "\n";

	$retailer_indexed += @$products;
}
print "$num_retailers pages, $retailer_indexed indexed\n" if ($args{v});

#
# retailers.html
#
print "info: gen retailers.html\n" if ($args{v});

$vars = { name => "Retailers", num => $num_retailers, links => $retailers };
$template->process("link_list.tt", $vars, "retailers.html")
	|| die "template: " . $template->error() . "\n";

#
# product_types/*
#
$sql = "select distinct type from products";
my $product_types = $dbh->selectcol_arrayref($sql);

my ($num_types, $type_indexed) = (scalar @$product_types, 0);
print "info: gen product_types/ " if ($args{v});
for my $type (@$product_types) {
	my $type_lc = lc($type);

	$sql = "select manufacturer, part_num from products where type = ?";
	my $products = $dbh->selectall_arrayref($sql, undef, $type);
	$_->[2] = get_description($_->[0], $_->[1]) for (@$products);

	my $vars = {
		name => $type, num => scalar @$products,
		products => $products,
	};
	$template->process("chart_list.tt", $vars, "product_types/$type_lc.html")
		|| die "template: " . $template->error() . "\n";

	$type_indexed += @$products;
}
print "$num_types pages, $type_indexed indexed\n" if ($args{v});

#
# product_types.html
#
print "info: gen product_types.html\n" if ($args{v});

$vars = { name => "Product_Types", num => $num_types, links => $product_types };
$template->process("link_list.tt", $vars, "product_types.html")
	|| die "template: " . $template->error() . "\n";

#
# products/*
#
$sql = "select * from products";
my $products = $dbh->selectall_hashref($sql, "part_num");

my $num_products = scalar keys %$products;
print "info: gen products/ $num_products pages\n" if ($args{v});
while (my ($part_num, $row) = each %$products) {
	my $part_num_lc = lc($part_num);
	my $manufacturer_lc = lc($row->{manufacturer});

	$row->{description} =
		get_description($row->{manufacturer}, $row->{part_num});

	# template makes directories if needed
	$template->process("product.tt", $row,
		"products/$manufacturer_lc/$part_num_lc.html")
		|| die "template: " . $template->error() . "\n";
}

# get a list of products added within the last week
$sql = "select manufacturer, part_num from products where first_seen > ?";
my $news = $dbh->selectall_arrayref($sql, undef, time - (7 * 24 * 60 * 60));

$sql = "select manufacturer, part_num from products where last_seen > ?";
my $upds = $dbh->selectall_arrayref($sql, undef, time - (2 * 60 * 60));

#
# index.html
#
$vars = {
	nret => $num_retailers, nmanuf => $num_manuf, nprod => $num_products,
	nnew => scalar @$news, news => $news, nupd => scalar @$upds, upds => $upds
};
print "info: gen index.html\n" if ($args{v});
$template->process("index.tt", $vars, "index.html")
	|| die "template: " . $template->error() . "\n";

$dbh->disconnect();

sub get_description
{
	my $manufacturer = shift;
	my $part_num = shift;

	my $sql = "select description from descriptions where " .
		"manufacturer = ? and part_num = ? order by date";
	my $descriptions = $dbh->selectcol_arrayref($sql, undef, $manufacturer,
		$part_num);

	# for now just return the first one ever scraped
	# XXX: leaving as is for cool future improvements (string interpolation!)
	return $descriptions->[0];
}
