#!/usr/bin/env perl

use strict;
use warnings;

# because we chroot all dependencies must be explicitly listed
use Config::Grammar;
use DBD::SQLite;
use Encode;
use FCGI;
use Getopt::Std;
use Template;
use Template::Context;
use Template::Filters;
use Template::Iterator;
use Template::Parser;
use Template::Plugins;
use Template::Stash::XS;
use PriceChart;
use Unix::Syslog qw(:macros :subs);
use URI::Escape;


my %args;
getopts("v", \%args);

# fork into background unless verbose
unless ($args{v}) {
	exit if (fork());
}

my $cfg = get_config();
my %http_cfg = %{$cfg->{"http"}};

# translates user/group names to id's, needs to be before chroot
my $uid_name = $http_cfg{"uid"};
my $gid_name = $http_cfg{"gid"};
my $uid = getpwnam($uid_name) or die "error: user $uid_name does not exist\n";
my $gid = getgrnam($gid_name) or die "error: group $gid_name does not exist\n";
print "info: $uid_name:$gid_name -> $uid:$gid\n" if ($args{v});;

# chroot early
print "info: chrooting to $http_cfg{chroot}\n" if ($args{v});
chroot($http_cfg{"chroot"});
chdir("/");

# XXX: verify we have indeed dropped privileges?
$( = $) = "$gid $gid";
$< = $> = $uid;
print "info: uid:gid set to $<:$(\n" if ($args{v});

print "info: opening syslog\n" if ($args{v});
openlog("pc_fcgi", LOG_PID, LOG_DAEMON);

my $socket_file = $http_cfg{"socket_file"};
if (-e $socket_file) {
	my $msg = "socket file $socket_file exists\n";
	print "error: $msg\n" if ($args{v});
	syslog(LOG_ERR, $msg);
	exit;
}

# XXX: i need to be sudo for this to work? after we've dropped privileges?
print "info: opening $socket_file\n" if ($args{v});
my $socket = FCGI::OpenSocket($socket_file, 1024);

my $db_dir = $http_cfg{"db_dir"};
print "info: opening $db_dir/pricechart.db\n" if ($args{v});
my $dbh = get_dbh($cfg->{"general"}, $db_dir);

my $request = FCGI::Request(\*STDIN, \*STDOUT, \*STDERR, \%ENV, $socket,
	FCGI::FAIL_ACCEPT_ON_INTR);

print "info: making template, include_path = $http_cfg{htdocs}\n" if ($args{v});
my $config = { INCLUDE_PATH => $http_cfg{"htdocs"} };
my $template = Template->new($config) || die $Template::ERROR . "\n";

# db query we take input from the user for
my $sql = "select part_num, manufacturer, description from products " .
	"where description like ? or part_num like ? or manufacturer like ?";
my $search_sth = $dbh->prepare($sql);

# intercept signals to shut down cleanly
$SIG{INT} =  \&child_sig;
$SIG{TERM} = \&child_sig;

syslog(LOG_INFO, "startup");
while ($request->Accept() >= 0) {
	print "Content-Type: text/html\r\n\r\n";
	my (undef, $input) = split("=", $ENV{QUERY_STRING});

	# incoming query string is http mangled
	$input = uri_unescape($input);

	$search_sth->execute("%$input%", "%$input%", "%$input%");
	my $products = $search_sth->fetchall_arrayref();

	my $vars = {
		query => $input,
		num_results => scalar @$products,
		results => $products
	};

	$template->process("search.tt2", $vars) or print $template->error();
}
syslog(LOG_INFO, "shutdown");
closelog();

$search_sth = undef;
$dbh->disconnect();

FCGI::CloseSocket($socket);
unlink($socket_file) or print "error: could not unlink $socket_file: $!";

sub child_sig
{
	my $signame = shift;

	$request->LastCall();
	print "info: caught SIG$signame\n" if ($args{v});
}
